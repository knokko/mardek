dependencies {
	for (dependency in project.ext.vulkanImplementationDependencies) implementation "$dependency"
	implementation "org.lwjgl:lwjgl-freetype"
	for (dependency in project.ext.vulkanRuntimeDependencies) runtimeOnly "$dependency"
	for (natives in project.ext.lwjglNatives) runtimeOnly "org.lwjgl:lwjgl-freetype::$natives"
	for (format in ["kim1", "kim2", "bc1", "bc4", "bc7"]) {
		implementation "com.github.knokko.vk-compressor:$format:$compressorVersion"
	}
	implementation files("../flash/ffdec_lib.jar")
	implementation project(":content")
	implementation project(":state")
	implementation project(":vk2d")
	implementation "org.apache.xmlgraphics:batik-all:1.19"
}

test {
	maxHeapSize = "4g"
}

tasks.register("exportContent", JavaExec) {
	group = "Export"
	description = "Imports the vanilla content, and exports it to game/src/main/resources/mardek/game"

	classpath sourceSets.main.runtimeClasspath
	mainClass = "mardek.importer.ContentExporterKt"
}

tasks.register("convertSVGs", JavaExec) {
	group = "Export"
	description = "Converts the Flash shapes from SVG to PNG. See the comments in " +
			"importer/converter/SvgShapeConverter.kt for more information."

	classpath sourceSets.main.runtimeClasspath
	mainClass = "mardek.importer.converter.SvgShapeConverterKt"
}

tasks.register("importRawAreaData", JavaExec) {
	group = "Export"
	description = "Extracts the raw area data from JPEX, " +
			"and exports it to importer/src/main/resources/mardek/importer/area/data-raw"

	classpath sourceSets.main.runtimeClasspath
	mainClass = "mardek.importer.area.AreaDumperKt"
}

tasks.register("randomID", JavaExec) {
	group = "Export"
	description = "Print some random UUIDs"
	classpath sourceSets.main.runtimeClasspath
	mainClass = "mardek.importer.ids.RandomIDKt"
}
