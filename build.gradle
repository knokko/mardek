import org.gradle.internal.os.OperatingSystem

buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'com.guardsquare:proguard-gradle:7.5.0'
	}
}

plugins {
	id "java"
	id "org.jetbrains.kotlin.jvm" version "2.0.0"
	id "com.github.johnrengelman.shadow" version "8.1.1"
}

allprojects {
	apply plugin: "java"
	apply plugin: "org.jetbrains.kotlin.jvm"

	java {
		withSourcesJar()
	}

	repositories {
		mavenCentral()
		maven { url 'https://jitpack.io' }
	}
	
	project.ext.jomlVersion = "1.10.1"
	project.ext.lwjglVersion = "3.3.4"
	project.ext.boilerVersion = "v4.5.0"
	project.ext.textRenderVersion = "v0.2.0"
	project.ext.compressorVersion = "v0.5.0"
	project.ext.bitserVersion = "0efc0df00f"
	switch (OperatingSystem.current()) {
		case OperatingSystem.LINUX:
			project.ext.lwjglNatives = [
					"natives-linux", "natives-linux-arm32", "natives-linux-arm64",
					"natives-linux-ppc64le", "natives-linux-riscv64"
			]
			break
		case OperatingSystem.WINDOWS:
			project.ext.lwjglNatives = ["natives-windows", "natives-windows-x86", "natives-windows-arm64"]
			break
		case OperatingSystem.MAC_OS:
			project.ext.lwjglNatives = ["natives-macos", "natives-macos-arm64"]
			break
		case OperatingSystem.FREE_BSD:
			project.ext.lwjglNatives = ["natives-freebsd"]
			break
	}

	dependencies {
		implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
		testImplementation platform("org.junit:junit-bom:5.10.3")
		testImplementation "org.junit.jupiter:junit-jupiter:5.10.3"
		testRuntimeOnly "org.junit.platform:junit-platform-launcher"
	}

	project.ext.vulkanImplementationDependencies = [
			"com.github.knokko:vk-boiler:$boilerVersion",
			"com.github.knokko.text-renderer:core:$textRenderVersion",
			"com.github.knokko.text-renderer:unicode-fonts:$textRenderVersion",
			"com.github.knokko.text-renderer:vulkan:$textRenderVersion",
			"org.joml:joml:$jomlVersion",
			"org.lwjgl:lwjgl",
			"org.lwjgl:lwjgl-vma",
			"org.lwjgl:lwjgl-vulkan",
	]
	project.ext.vulkanRuntimeDependencies = project.ext.lwjglNatives.collectMany {
		def result = ["org.lwjgl:lwjgl::$it", "org.lwjgl:lwjgl-vma::$it", "org.lwjgl:lwjgl-freetype::$it"]
		if (it.startsWith("natives-macos")) result.add("org.lwjgl:lwjgl-vulkan::$it")
		result
	}

	test {
		useJUnitPlatform()
	}
}
