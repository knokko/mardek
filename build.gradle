buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath "com.guardsquare:proguard-gradle:7.7.0"
	}
}

plugins {
	id "java"
	id "org.jetbrains.kotlin.jvm" version "2.2.21"
	id "com.gradleup.shadow" version "8.3.6"
}

allprojects {
	apply plugin: "java"
	apply plugin: "org.jetbrains.kotlin.jvm"

	java {
		withSourcesJar()
	}

	repositories {
		mavenCentral()
		maven { url = "https://central.sonatype.com/repository/maven-snapshots" }
		maven { url = "https://jitpack.io" }
	}

	project.ext.jomlVersion = "1.10.8"
	project.ext.lwjglVersion = "3.4.0-SNAPSHOT"
	project.ext.boilerVersion = "e252b1b971b5a11949ee905ec23b63a55e278529"
	project.ext.compressorVersion = "ac0cc60e2a9fa42c1d852d978028eb2777fc4d0a"
	project.ext.bitserVersion = "685d0d2aee3ccb44072a828a787cdff66eaf87b8"
	project.ext.lwjglNatives = [
			"natives-linux", "natives-linux-arm32", "natives-linux-arm64",
			"natives-linux-ppc64le", "natives-linux-riscv64", "natives-freebsd",
			"natives-windows", "natives-windows-x86", "natives-windows-arm64",
			"natives-macos", "natives-macos-arm64"
	]

	dependencies {
		implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
		testImplementation platform("org.junit:junit-bom:5.12.2")
		testImplementation "org.junit.jupiter:junit-jupiter:5.12.2"
		testRuntimeOnly "org.junit.platform:junit-platform-launcher"
	}

	project.ext.vulkanImplementationDependencies = [
			"com.github.knokko:vk-boiler:$boilerVersion",
			"org.joml:joml:$jomlVersion",
			"org.lwjgl:lwjgl",
			"org.lwjgl:lwjgl-vulkan",
	]
	project.ext.vulkanRuntimeDependencies = project.ext.lwjglNatives.collectMany {
		def result = ["org.lwjgl:lwjgl::$it", ]
		if (it.startsWith("natives-macos")) result.add("org.lwjgl:lwjgl-vulkan::$it")
		result
	}

	test {
		useJUnitPlatform()
		minHeapSize = "512m" // initial heap size
		maxHeapSize = "2048m" // maximum heap size
	}
}
